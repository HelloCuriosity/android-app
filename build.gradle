// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    apply from: 'dependency.gradle'

    repositories {
        google()
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.2'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.61'
        classpath 'com.dicedmelon.gradle:jacoco-android:0.1.4'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "org.jmailen.kotlinter" version "2.1.3"
    id "io.gitlab.arturbosch.detekt" version "1.2.1"
    id "org.sonarqube" version "2.8"
}

sonarqube {
    def flavorName = ""
    def buildType = ""
    if (project.hasProperty("flavor")) {
        flavorName = flavor
        buildType = "Release"
        androidVariant flavorName + buildType
    } else {
        buildType = "Debug"
        androidVariant buildType
    }

    properties {
        property "sonar.projectName", "Android App Template"
        property "sonar.projectKey", "curious-coding_android-app"
        property "sonar.java.source", "8"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.language", "java"

        property "sonar.java.binaries", "build/tmp/kotlin-classes/${flavorName}${buildType.capitalize()}"
        property "sonar.java.test.binaries", "build/tmp/kotlin-classes/${flavorName}${buildType.capitalize()}"

        property "sonar.junit.reportsPath", "build/test-results/test${flavorName.capitalize()}${buildType.capitalize()}UnitTest"
        property "sonar.jacoco.reportPaths", "build/jacoco/test${flavorName.capitalize()}${buildType.capitalize()}UnitTest.exec"

        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.exclusions", "**/*.js,**/*.css,**/*.html"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://maven.google.com" }
        maven { url "https://jitpack.io" }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    ciBuild = System.getenv("TRAVIS") == "true"
    ciBuildNr = System.getenv("TRAVIS_BUILD_NUMBER")
}