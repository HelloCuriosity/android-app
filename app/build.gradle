def versionMajor = 1
def versionMinor = 0
def versionPatch = 0

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply from: rootProject.file('quality/quality-gate.gradle')

android {
    compileSdkVersion 30
    buildToolsVersion "29.0.3"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.hello.curiosity"
        minSdkVersion 19
        targetSdkVersion 30

        versionCode 1
        if (ciBuild) {
            versionCode = ciBuildNr as Integer
        }
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        multiDexEnabled true
        testInstrumentationRunner "com.hello.curiosity.template.test.runner.TestRunner"
    }

    signingConfigs {
        release {
            storeFile rootProject.file("keystore/android-app.keystore")
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias "android-app"
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            testCoverageEnabled true
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "all"
    productFlavors {
        production {
            dimension "all"
        }
        staging {
            dimension "all"
            versionNameSuffix "-staging"
            applicationIdSuffix ".staging"
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/rxjava.properties'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Android
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'com.google.android.material:material:1.2.1'

    // Kotlin
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.72'

    // Hello Curiosity
    implementation 'com.github.curious-coding:android-animations:0.4.0'

    // RxJava
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava3:rxjava:3.0.4'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.5'

    // Testing
    testImplementation 'androidx.test:core:1.2.0'

    // JUnit
    testImplementation 'androidx.test.ext:junit:1.1.2'

    // Mockito
    testImplementation 'org.mockito:mockito-core:3.4.4'
    testImplementation('com.nhaarman:mockito-kotlin:1.5.0') {
        exclude group: 'org.mockito', module: 'mockito-core'
    }

    // Robolectric
    testImplementation('org.robolectric:robolectric:4.4') {
        exclude module: 'classworlds'
        exclude module: 'commons-logging'
        exclude module: 'httpclient'
        exclude module: 'maven-artifact'
        exclude module: 'maven-artifact-manager'
        exclude module: 'maven-error-diagnostics'
        exclude module: 'maven-model'
        exclude module: 'maven-project'
        exclude module: 'maven-settings'
        exclude module: 'plexus-container-default'
        exclude module: 'plexus-interpolation'
        exclude module: 'plexus-utils'
        exclude module: 'wagon-file'
        exclude module: 'wagon-http-lightweight'
        exclude module: 'wagon-provider-api'
        exclude group: 'com.google.auto.service', module: 'auto-service'
    }

    // Instrumentation tests

    // Android
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'

    // Espresso
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
