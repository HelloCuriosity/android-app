apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.7.9"
    reportsDir = file("$buildDir/reports")
}

task jacocoTestReport(type: JacocoReport, dependsOn: "testDebugUnitTest") {
    group = "Reporting"
    description = "Generate Jacoco coverage reports for Debug build"

    reports {
        xml.enabled = true
        html.enabled = true
    }

    // what to exclude from coverage report
    // UI, "noise", generated classes, platform classes, etc.
    getClassDirectories().from(
            fileTree(
                    dir: "$buildDir/tmp/kotlin-classes/debug",
                    excludes: ['**/R.class',
                               '**/R$*.class',
                               '**/*$ViewBinder*.*',
                               '**/*$InjectAdapter*.*',
                               '**/*Injector*.*',
                               '**/BuildConfig.*',
                               '**/Manifest*.*',
                               '**/*Test*.*',
                               '**/*Activity*.*',
                               '**/*Fragment*.*',
                               'android/**/*.*'
                    ])
    )

    // sources
    getSourceDirectories().from(
            files([
                    android.sourceSets.main.java.srcDirs,
                    "src/main/java"
            ])
    )

    getExecutionData().from(
            files("$buildDir/jacoco/testDebugUnitTest.exec")
    )
}